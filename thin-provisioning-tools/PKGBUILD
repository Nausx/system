# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=0.7.4
pkgrel=1
pkgdesc='Suite of tools for manipulating the metadata of the dm-thin device-mapper target'
arch=('i686' 'x86_64')
url="https://github.com/jthornber/thin-provisioning-tools"
license=('GPL3')
depends=('expat' 'gcc-libs' 'libaio')
makedepends=('boost')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jthornber/${pkgname}/archive/v${pkgver}.tar.gz"
        '0001-fix-build-with-testing-disabled.patch')
sha256sums=('f65f84e653e182480dacad77c611e13cd6de1c354e02fb4b96db4d2f328c3904'
            '8f59d9076ed23d593f8671792804cbfeb8bdae5c996200e1269f27fa49a6b873')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 < "${srcdir}"/0001-fix-build-with-testing-disabled.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR="${pkgdir}"/usr/bin MANDIR=/usr/share/man install
}

