# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=bash
_basever=4.4
_patchlevel=012
pkgver=${_basever}.${_patchlevel}
pkgrel=3
pkgdesc='The GNU Bourne Again shell'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.gnu.org/software/bash/bash.html'
groups=('base')
backup=(etc/bash/bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline>=7.0' 'glibc' 'ncurses')
optdepends=('bash-completion: for tab completion')
provides=('sh')
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout
        artix.bashrc
        functions.bashrc)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
  done
fi

prepare() {
  cd $pkgname-$_basever

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    msg "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
  done
}

build() {
  cd $pkgname-$_basever

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/sbin:/usr/local/bin:/usr/bin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
               -DSYS_BASHRC=\'\"/etc/bash/bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash/bash_logout\"\'
               -DNON_INTERACTIVE_LOGIN_SHELLS)
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/usr --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline
  make
}

check() {
  make -C $pkgname-$_basever check
}

package() {
  make -C $pkgname-$_basever DESTDIR="$pkgdir" install
  ln -s bash "$pkgdir"/usr/bin/sh

  install -dm755 "$pkgdir"/etc/{skel,bash/bashrc.d}/
  # system-wide configuration files
  install -m644 system.bashrc $pkgdir/etc/bash/bashrc
  install -m644 system.bash_logout "$pkgdir"/etc/bash/bash_logout

  # artix bashrc
  install -m644 artix.bashrc "$pkgdir"/etc/bash/bashrc.d/artix.bashrc
  install -m644 functions.bashrc "$pkgdir"/etc/bash/bashrc.d/functions.bashrc

  # user configuration file skeletons
  install -m644 dot.bashrc "$pkgdir"/etc/skel/.bashrc
  install -m644 dot.bash_profile "$pkgdir"/etc/skel/.bash_profile
  install -m644 dot.bash_logout "$pkgdir"/etc/skel/.bash_logout
}
sha256sums=('d86b3392c1202e8ff5a423b302e6284db7f8f435ea9f39b5b1b20fd3ac36dfcb'
            'SKIP'
            'b39df263fa05d29e7a45a163c6fb3cdd82d9492194797ca4e959c0cf3ab4645b'
            'e149407c2bee17779caec70a7edd3d0000d172e7e4347429b80cb4d55bcec9c2'
            '4330edf340394d0dae50afb04ac2a621f106fe67fb634ec81c4bfb98be2a1eb5'
            '6c4af5d9c12c5fb7dcc7e4c2848482f59fe2272ffdeabd7462ab861d2f5d2548'
            'd6030e415c64ab0133d9592250c05745435dabee29935b21a599c4215fab3ae0'
            '2a31b6d1b161125222981f3d44f468ef2a73deee94c553f41480f4ae944bdcc5'
            '88bdde3d77172af7bb4688f8597e84bc5895ff0552d3ffd296fda3cf51354e78'
            '3e28d91531752df9a8cb167ad07cc542abaf944de9353fe8c6a535c9f1f17f0f'
            'SKIP'
            '7020a0183e17a7233e665b979c78c184ea369cfaf3e8b4b11f5547ecb7c13c53'
            'SKIP'
            '51df5a9192fdefe0ddca4bdf290932f74be03ffd0503a3d112e4199905e718b2'
            'SKIP'
            'ad080a30a4ac6c1273373617f29628cc320a35c8cd06913894794293dc52c8b3'
            'SKIP'
            '221e4b725b770ad0bb6924df3f8d04f89eeca4558f6e4c777dfa93e967090529'
            'SKIP'
            '6a8e2e2a6180d0f1ce39dcd651622fb6d2fd05db7c459f64ae42d667f1e344b8'
            'SKIP'
            'de1ccc07b7bfc9e25243ad854f3bbb5d3ebf9155b0477df16aaf00a7b0d5edaf'
            'SKIP'
            '86144700465933636d7b945e89b77df95d3620034725be161ca0ca5a42e239ba'
            'SKIP'
            '0b6bdd1a18a0d20e330cc3bc71e048864e4a13652e29dc0ebf3918bea729343c'
            'SKIP'
            '8465c6f2c56afe559402265b39d9e94368954930f9aa7f3dfa6d36dd66868e06'
            'SKIP'
            'dd56426ef7d7295e1107c0b3d06c192eb9298f4023c202ca2ba6266c613d170d'
            'SKIP'
            'fac271d2bf6372c9903e3b353cb9eda044d7fe36b5aab52f21f3f21cd6a2063e'
            'SKIP')
